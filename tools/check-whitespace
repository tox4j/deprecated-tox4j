#!/usr/bin/env perl

use strict;
use File::Find;

my %errors;

sub e {
   my ($msg, $line) = @_;
   chomp $line;
   $errors{$File::Find::name}{$msg} = $line;
}

sub e_tab {
   my ($line) = @_;
   e "contains tab", $line;
}

sub e_ws_eol {
   my ($line) = @_;
   e "contains white-space at EOL", $line;
}

sub e_no_eol {
   my ($line) = @_;
   e "has no new-line at EOF", $line;
}

sub check(&) {
   my ($cb) = @_;
   sub {
      open my $fh, '<', $_[0]
         or die "Could not open $_[0] for reading: $!";
      for my $line (<$fh>) {
         local $_ = $line;
         $cb->();
      }
   }
}

*check_tab = check { e_tab $_ if /\t/ };
*check_ws_eol = check { e_ws_eol $_ if /\s\n/ };
*check_no_eol = check { e_no_eol $_ unless /\n$/ };

find {
   wanted => sub {
      check_ws_eol ($_);
      check_no_eol ($_);

      return if /^Makefile.*/
         or $_ eq "CMakeLists.txt"
         or $_ eq "Build.lsp";

      check_tab ($_);
   },
}, 'src';

if (%errors) {
   for my $file (sort keys %errors) {
      for my $msg (sort keys $errors{$file}) {
         my $line = $errors{$file}{$msg};
         print "$file: error: file $msg\n";
         print "  at: '$line'\n";
      }
   }

   exit 1
}
